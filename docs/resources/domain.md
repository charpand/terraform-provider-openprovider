---
page_title: "openprovider_domain Resource - terraform-provider-openprovider"
subcategory: ""
description: |-
  Manages an OpenProvider domain.
---

# openprovider_domain (Resource)

Manages an OpenProvider domain.

## Example Usage

### Basic

```terraform
resource "openprovider_domain" "example" {
  domain       = "example.com"
  owner_handle = "owner123"
  period       = 1
}
```

### With Customer Handle

```terraform
# Create a customer for the domain owner
resource "openprovider_customer" "domain_owner" {
  email = "owner@example.com"

  phone {
    country_code = "1"
    area_code    = "555"
    number       = "1234567"
  }

  address {
    street  = "Main Street"
    number  = "100"
    city    = "New York"
    state   = "NY"
    zipcode = "10001"
    country = "US"
  }

  name {
    first_name = "John"
    last_name  = "Doe"
  }
}

# Register a domain using the customer handle
resource "openprovider_domain" "example" {
  domain       = "example.com"
  owner_handle = openprovider_customer.domain_owner.handle
  period       = 1
}
```

### With NS Group (Recommended)

```terraform
resource "openprovider_nsgroup" "my_nameservers" {
  name = "cloudflare-ns"

  nameservers {
    name = "ns1.cloudflare.com"
  }

  nameservers {
    name = "ns2.cloudflare.com"
  }
}

resource "openprovider_domain" "example" {
  domain       = "example.com"
  owner_handle = "owner123"
  period       = 1
  ns_group     = openprovider_nsgroup.my_nameservers.name
}
```

### Full (Legacy Nameservers)

```terraform
resource "openprovider_domain" "prod" {
  domain         = "mydomain.com"
  owner_handle   = "owner123"
  admin_handle   = "admin456"
  tech_handle    = "tech789"
  billing_handle = "bill001"
  period         = 2
  autorenew      = true

  nameserver {
    hostname = "ns1.example.com"
  }

  nameserver {
    hostname = "ns2.example.com"
  }
}
```

<!-- schema generated by tfplugindocs -->
## Schema

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `domain` (String) The domain name (e.g., example.com).
- `owner_handle` (String) The owner contact handle for the domain.

### Optional

- `admin_handle` (String) The admin contact handle for the domain.
- `autorenew` (Boolean) Whether the domain should auto-renew.
- `billing_handle` (String) The billing contact handle for the domain.
- `nameserver` (Block List, Deprecated) List of nameservers for the domain. **Deprecated:** Use `ns_group` instead. (see [below for nested schema](#nestedblock--nameserver))
- `ns_group` (String) The nameserver group to use for this domain. Use this instead of nameserver blocks.
- `period` (Number) Registration period in years.
- `tech_handle` (String) The tech contact handle for the domain.

### Read-Only

- `id` (String) The domain identifier (domain name).
- `status` (String) The current status of the domain.

<a id="nestedblock--nameserver"></a>
### Nested Schema for `nameserver`

Required:

- `hostname` (String) The hostname of the nameserver (e.g., ns1.example.com).




## Import

Import a domain using the domain name.

```shell
$ terraform import openprovider_domain.example example.com
```
